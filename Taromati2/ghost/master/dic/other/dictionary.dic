/////////
/////////英漢互查字典
/////////written by KikkaAIdb.[2009-12-29][2010-3-15改字典]★
//----------------------------------------------------------------------------------------------------------------
Select.dictionary{
	if QuickStartVersion{
		NeedUpdate
		return
	}
	"\0\s[0]\_q請輸入需要查詢的詞語!  橘花知道8萬詞條哦~\n可以是英文也可以是中文!(使用模糊查詢)\n[150]提示: 小寫數位會被忽略，當存在中文時忽略英文！\n[150]\![*]\q[返回上一層,OnOpenMenu,1]\n\![*]\q[取消,Onoverdic,Ondict]\![open,inputbox,Ondict,-1,%(Dicword[0])]"
}
Onoverdic{
	"好的...\![close,inputbox,%(reference0)]"
}
Ondict{
	_w=''
	_j=0
	_p=1
	_a=CUTSPACE(reference0)
	if _argv[0] {
		_p=TOINT(_argv[0])
		_a=Dicword[0]
	}
	Dicword[0]=_a
	_a=TOLOWER(_a)
	_a = RE_REPLACE(_a,'[ ]','-')
	//_b = RE_REPLACE(_a,'\w',NULL)//用這句有時候會不起作用,原因未知
	_b = RE_REPLACE(_a,'[abcdefghijklmnopqrstuvwxyz1234567890-]',NULL)
	//_a = RE_REPLACE(_a,'\d',NULL)
	_a = RE_REPLACE(_a,'[0123456789]',NULL)
	if _b
		_j=SQLite('-f',"SELECT * FROM dictionary WHERE ONE LIKE'%%(_b)%'")
	elseif STRLEN(_a)==1
		_j=SQLite('-f',"SELECT * FROM dictionary WHERE ONE LIKE'%(_a)|%'")
	elseif _a
		_j=SQLite('-f',"SELECT * FROM dictionary WHERE ONE LIKE'%(_a)%'")
	_tola=(_j+7)/8
	Dicword[1]=_tola
	for _i=(_p-1)*8;_i<_j && _i<(_p-1)*8+8;_i++ {
		_t=EVAL('valueex' + _i)
		_Endic=_t[0,'|']
		_Cdic='複製該詞'
		if _Endic==_argv[1]
			_Cdic='\f[color,240,20,220]已複製啦♪\f[default]'
		_w+="\f[bold,true]%(_Endic)\f[bold,false]\_l[150,-]\![*]\__q[OnCopydic,%(_Endic),%(_p)]%(_Cdic)\__q\n"+_t[1,'|']+'\n[130]'
	}
	if !_a {
		Select.dictionary
	}
	else {
		if _j==0 {
			if STRLEN(_a)==1
				"\0\s[0] %(reference0) 嗎?\n只是個字母吧,還有其他含義嗎...\n[130]\![*]\q[繼續搜索,dictionary]\n\![set,choicetimeout,36000]\![*]\q[返回上一層,OnOpenMenu,0]\n\![*]\q[取消,over00]"
			else
				"\0\s[0]沒有找到呢...\n[130]%(Select.dictionary)"
		}
		--
		"\0\s[26]\b[2]\_q符合記錄條目為%(_j),以下為第%(_p*8-7)-%(MIN(_p*8,_j))條結果:\n[150]%(_w)\n[50]"
		--
		if _p!=1
			"\_l[20,-]\q[<<上一頁,OndicPage,%(_p-1)]"
		--
		"\_l[93,-]\q[◇第%(_p)頁,Ondicselepag]    共%(_tola)頁"
		--
		if _p*8<_j
			"\_l[200,-]\q[下一頁>>,OndicPage,%(_p+1)]"
		--
		'\n[150]\![*]\q[繼續搜索,dictionary]\![set,choicetimeout,36000]\n\![*]\q[返回上一層,OnOpenMenu,0]  \![*]\q[取消,Onoverdic]'
	}
}
OndicPage{
	Ondict(reference0)
}
Ondicselepag{
	"\0\s[0]\_q\![set,choicetimeout,20000]請輸入頁數（半形數位）,共有 %(Dicword[1]) 頁!(只顯示前125頁)\n[200]\![*]\q[取消,Onoverdic,Ondicsp]\![open,inputbox,Ondicsp,20000]"
}
Ondicsp{//選頁
	_a=CUTSPACE(reference0)
	_b=RE_REPLACE(_a,'[0123456789]',NULL)
	if _b || !_a {
		Ondicselepag
	}
	else {
		_a=TOINT(_a)
		if _a>Dicword[1] || _a>125
			"\0\s[0]輸入的頁數過大,總頁數%(Dicword[1])(只顯示前125頁),請重新輸入~\w8\w8\c%(Ondicselepag)"
		elseif _a<=0 {
			Ondicselepag
		}
		else {
			Ondict(_a)
		}
	}
}
OnCopydic{
	void OnCopy(reference0)
	"\0\s[0]\b[2]已成功複製單詞 『%(reference0)』 到剪貼板了喔♪\n%(Ondict(reference1,reference0))"
}
