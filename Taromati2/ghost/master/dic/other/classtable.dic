///////////////////////////////////////////
//////////課程表功能辭書 v2.0
//////////written by forjane 2009.09.02
///////////////////////////////////////////
ClassTableVarInit{
	classdata = IARRAY(56)
	classdetail = IARRAY(56)
	classday = 0
	classtime = 0
	classfullweek = 0
	classcollege = 0
}
OnClassTable{
	classtimerm=5
	if classtableinitialized == 1 {
		ClassTable(0)
	}
	else {
		ClassTableVarInit
		classtableinitialized = 1
		'\0\s[26]第一次使用課程表嗎？\w9\w9\n點擊課程可以查看和修改。\w9\w9那麼，\w9先把課程填好吧…\w9…\w9\n\w9\w9\c'
		--
		ClassTable(0)
	}
}
ClassTable{
	_days = 0
	_times = 0
	_txt =''
	_title = ''
	_classtxt = '\n[550]'
	_tempclassdata = '<無>'
	_tempclassdetail = '<無>'
	_button1 = '添加'
	_button2 = '添加'
	if _argv[0] == 1 {
		if classdata[classtime * 7 + classday] != '' {
			_tempclassdata = classdata[classtime * 7 + classday]
			_tempclassdata = '\f[bold,true]' + _tempclassdata[0,'='] + '\f[bold,default]'
			_button1 = '修改'
		}
		if classdetail[classtime * 7 + classday] != '' {
			_tempclassdetail = classdetail[classtime * 7 + classday]
			_button2 = '修改'
		}
		_classtxt = '\n課程名稱：                        ' + '\q[◇' + _button1 + '課程,OnSetClass,' + classtime +','+ classday + ']\n' + _tempclassdata + '\n[150]'
		_classtxt += '備註（時間地點等）：         ' + '\q[◇' + _button2 + '備註,OnSetClassDetail,' + classtime +','+ classday + ']\n' + _tempclassdetail +'\n'
	}
	_outform = "\0\b[2]\s[40]\n\_q%(year)年%(month)月%(day)日（星期%(DayofWeek_w)） %(hour)點 %(minute)分\n[150]"
	if classcollege == 1 {
		_times = 6
		_outform += '\q[◆大學模式,OnCheckCollege,1]・\q[◇中學模式,OnCheckCollege,0]   '
	}
	else {
		_times = 8
		_outform += '\q[◇大學模式,OnCheckCollege,1]・\q[◆中學模式,OnCheckCollege,0]   '
	}
	if classfullweek {
		_days = 7
		_title = '週一  週二  週三  週四  週五  \f[color,255,0,128]週六  周日\f[color,default]'
		_outform += '\q[◆七天,OnCheckFullWeek,1]・\q[◇五天,OnCheckFullWeek,0]\n'
	}
	else {
		_days = 5
		_title = '星期一  星期二  星期三  星期四  星期五'
		_outform += '\q[◇七天,OnCheckFullWeek,1]・\q[◆五天,OnCheckFullWeek,0]\n'
	}
	for _j = 0;_j < _times;_j++ {
		for _i = 0;_i < _days;_i++ {
			_colortxt = ModifyData(classdata[_j * 7 + _i], _j, _i)
			if classdetail[_j * 7 + _i] != '' {
				_colortxt = '\f[underline,true]' + _colortxt + '\f[underline,default]'
			}
			if _argv[0] == 1 && _i == classday && _j == classtime {
				_colortxt = '\f[bold,true]' + _colortxt + '\f[bold,default]'
			}
			_txt += _colortxt
		}
		if _j == 3 || _j == 1 && classcollege == 1 {
			_txt += '\n┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈┈'
		}
		_txt += '\n'
	}
	_outform += "━━━━━━━━━━━━━━━━━━\n/
	%(_title)\n──────────────────\n/
	%(_txt)━━━━━━━━━━━━━━━━━━"
	_outform += _classtxt
	_outform += '\n\q[◇返回上一層,OnOpenMenu,0]\n/
	\q[◇終了,OnClassEnd]\_q\![set,choicetimeout,0]'
	_outform
}
OnClassEnd{
	'\0\s[5]好的。那麼…學習上、也請多多加油喔。'
}
OnCheckFullWeek{
	classfullweek = reference0
	ClassTable(0)
}
OnCheckCollege{
	classcollege = reference0
	ClassTable(0)
}
OnShowDetail{
	classday = reference1
	classtime = reference0
	ClassTable(1)
}
ModifyData{
	_txt = _argv[0]
	_posx = 0
	_txtout = ''
	if _txt[1,'='] != '' {
		_txt = _txt[1,'=']
	}
	if classfullweek == 1 {
		if _txt == '' {
			_txt = '<無>'
			_posx = _argv[2] * 36 - 3
		}
		else {
			_txt = SUBSTR(_txt,0,2)
			_posx = _argv[2] * 36 + 12 - STRLEN(_txt) * 6
		}
	}
	else {
		if _txt == '' {
			_txt = '<無>'
			_posx = _argv[2] * 46 + 3
		}
		else {
			_txt = SUBSTR(_txt,0,3)
			_posx = _argv[2] * 46 + 18 - STRLEN(_txt) * 6
		}
	}
	_txtout += '\_l[' + _posx + ']'
	_txtout += '\q[' + _txt + ',OnShowDetail,' + _argv[1] +','+ _argv[2] +']'
	_txtout
}
OnSetClass{
	classday = reference1
	classtime = reference0
	_dayOfWeek = ('一','二','三','四','五','六','日')
	_when = ('上午','下午','晚上','第1','第2','第3','第4','1-2','3-4')
	_ampm = _when[classtime / (4 - 2 * classcollege)]
	_num = _when[classtime % (4 - 2 * classcollege) + 3 + 4 * classcollege]
	_temp = classdata[classtime * 7 + classday]
	"\0\s[40]要修改星期%(_dayOfWeek[classday])" + _ampm + _num + "節課的內容嗎？\n請輸入課程名稱（可以用“全稱=縮寫”格式來指定課程縮寫，不指定縮寫則預設顯示前幾個字元）…/
	\![open,inputbox,OnSetClassFinish,0,%(_temp)]"
}
OnSetClassDetail{
	classday = reference1
	classtime = reference0
	_dayOfWeek = ('一','二','三','四','五','六','日')
	_when = ('上午','下午','晚上','第1','第2','第3','第4','1-2','3-4')
	_ampm = _when[classtime / (4 - 2 * classcollege)]
	_num = _when[classtime % (4 - 2 * classcollege) + 3 + 4 * classcollege]
	_temp = classdata[classtime * 7 + classday]
	if _temp != '' {
		_temp = _temp[0,'=']
		_temp2 = classdetail[classtime * 7 + classday]
		"\0\s[40]要修改星期%(_dayOfWeek[classday])" + _ampm + _num + "節%(_temp)課的說明嗎？\n[150]請輸入課程說明（時間，地點，授課老師，注意事項等）…/
		\![open,inputbox,OnSetClassDetailFinish,0,%(_temp2)]"
	}
	else {
		'\0\s[3]請先填好課程名稱……\w9\w9\c'
		--
		ClassTable(1)
	}
}
OnSetClassFinish{
	classdata[classtime * 7 + classday]	= SUBSTR(reference0, 0, 12)
	if reference0 == '' {
		classdetail[classtime * 7 + classday] = ''
	}
	ClassTable(1)
}
OnSetClassDetailFinish{
	classdetail[classtime * 7 + classday] = SUBSTR(reference0, 0, 35)
	ClassTable(1)
}
OnClassDetailTimeCheck{
	_timetick=secondnowtime(hour,minute,second)
	_days = 0
	_times = 0
	if classcollege == 1 {
		_times = 6
	}
	else {
		_times = 8
	}
	if classfullweek {
		_days = 7
	}
	else {
		_days = 5
	}
	_week='一,二,三,四,五,六,日'
	_txt=''
	_rmflag=0
	for _j = 0;_j < _times;_j++ {
		for _i = 0;_i < _days;_i++ {
			if classdetail[_j * 7 + _i] != '' &&DayofWeek_w==_week[ _i]{
				_classdetail=SPLIT(classdetail[_j * 7 + _i],',')
				if ':' _in_ _classdetail[0] {
					_time=SPLIT(_classdetail[0],':')
					_hour=_time[0]
					_minute=_time[1]
					_second=_time[2]
					_timeleft=secondnowtime(_hour,_minute,_second)
					if (ABS(_timeleft-_timetick) <= classtimerm*60) && (_timeleft - _timetick>0){
						_rmflag=1
						_txt='馬上開始上課咯。科目：'+classdata[_j * 7 + _i]+'，備註：'+classdetail[_j * 7 + _i]
					}
					elseif (ABS(_timeleft-_timetick) <= classtimerm*60) && (_timeleft - _timetick<=0){
						_rmflag=1
						_txt='已經開始上課咯。科目：'+classdata[_j * 7 + _i]+'，備註：'+classdetail[_j * 7 + _i]
					}
				}
			}
		}
	}
	if _rmflag{
		if !ClassMonitorTempClose
			_txt
		else
			''
	}
	else
		ERASEVAR('ClassMonitorTempClose')
}
OnClassMonitorTempClose{
	ClassMonitorTempClose=1
}
secondnowtime{
	_hour=TOINT(_argv[0])
	_minute=TOINT(_argv[1])
	_second=TOINT(_argv[2])
	_hour*3600+_minute*60+_second
}
